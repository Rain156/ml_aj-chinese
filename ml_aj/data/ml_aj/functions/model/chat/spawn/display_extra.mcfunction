## function ml_aj:model/chat/display
## player context
## "id": The model's UUID string
## "name": The save name
## "suggest": The build command
## "despawn": The despawn type

function ml_aj:system/player/chat_clear

$tellraw @s [{"text":"","color":"#C29C54"},{"text":"---------------- Save & Spawn ----------------\n\n"},{"text":" Save Name: ","hoverEvent":{"action":"show_text","contents":"The name to save this model as"}},{"text":"$(name)","color":"yellow","hoverEvent":{"action":"show_text","contents":"Click to change the name that is used to save this model for spawning (When the command shows up in your chat, edit the value between the quotation marks, and press Enter)"},"clickEvent":{"action":"suggest_command","value":"/function ml_aj:edit/save {name:\"$(name)\"}"}},{"text":"\n\n"},{"text":" [Save] ","color":"dark_green","hoverEvent":{"action":"show_text","contents":"Click to save the model data for spawning"},"clickEvent":{"action":"run_command","value":"/function ml_aj:model/chat/spawn/save {id:\"$(id)\",name:\"$(name)\"}"}},{"storage":"ml_aj:logic","nbt":"data.player.temp.edited","interpret":true},{"text":"\n\n"},{"text":" Despawn Action: ","hoverEvent":{"action":"show_text","contents":"The action to take if a player is not within range of this model. N/A = no despawn action. Disable = disable model when player out of range. Delete = delete model when player out of range."}},{"text":" [$(despawn)] ","color":"aqua","hoverEvent":{"action":"show_text","contents":"Click to Toggle"},"clickEvent":{"action":"run_command","value":"/function ml_aj:model/chat/spawn/edit_despawn {id:\"$(id)\",despawn:\"$(despawn)\"}"}},{"storage":"ml_aj:logic","nbt":"data.player.temp.despawn_text","interpret":true}]

$execute if data storage ml_aj:logic data.spawn.models[{name:"$(name)"}] run tellraw @s {"text":"\n---------------- Spawn Method ----------------\n","color":"#C29C54"}
$execute if data storage ml_aj:logic data.spawn.models[{name:"$(name)"}] run tellraw @s [{"text":"","color":"#C29C54"},{"text":" [Command] ","color":"light_purple","hoverEvent":{"action":"show_text","contents":"Click to get the command that will spawn in this model (make sure you click Save first if changes were made)"},"clickEvent":{"action":"suggest_command","value":"/function ml_aj:spawn {name:\"$(name)\"}"}},{"text":"  |  "},{"text":" [Radius Command] ","color":"#ca8ff0","hoverEvent":{"action":"show_text","contents":"Click to get the command that will spawn in this model within a random distance from the target position (make sure you click Save first if changes were made)"},"clickEvent":{"action":"suggest_command","value":"/execute positioned ~ ~ ~ run function ml_aj:spawn_radius {radius:5,name:\"$(name)\"}"}},{"text":"  |  "},{"text":" [Item] ","color":"dark_purple","hoverEvent":{"action":"show_text","contents":"Click to get the item that will spawn in this model (make sure you click Save first if changes were made)"},"clickEvent":{"action":"run_command","value":"/function ml_aj:model/chat/spawn/item {id:\"$(id)\",name:\"$(name)\"}"}}]

$tellraw @s [{"text":"","color":"#C29C54"},{"text":"\n---------------------------------------------\n\n"},{"text":" [Datapack Command] ","color":"dark_aqua","hoverEvent":{"action":"show_text","contents":"Click to copy the full command that will spawn this mob. This is useful when you are creating a custom mob datapack where you would include the ml_aj datapack, and just use this command to spawn the mob without needing to set any storage data. The radius option in the command is the max random distance to spawn the mob from the target location."},"clickEvent":{"action":"copy_to_clipboard","value":"/function ml_aj:model/spawn/build {radius:0,data:$(suggest)}"}},{"text":"  |  "},{"text":" [Return] ","color":"#DD743B","hoverEvent":{"action":"show_text","contents":"Return to the AJ Model Editor menu"},"clickEvent":{"action":"run_command","value":"/function ml_aj:model/chat/display {id:\"$(id)\"}"}},{"text":"\n"}]
